-- The reference semantics are taken from https://www.felixcloutier.com/x86/,
-- which itself is just extracted from https://www.intel.com/content/www/us/en/developer/articles/technical/intel-sdm.html

import Lean
import Std
import Kraken.Syntax

-- injective coercions only
attribute [-instance] BitVec.instNatCast
attribute [-instance] BitVec.instIntCast
instance : Coe Bool Nat where coe := Bool.toNat

def BitVec.take {w} (x : BitVec w) (n : Nat) : BitVec n := x.extractLsb' 0 n
def BitVec.drop {w} (x : BitVec w) (n : Nat) : BitVec (w - n) := x.extractLsb' n (w-n)
def BitVec.replaceLow {w n} (old : BitVec w) (new : BitVec n) : BitVec w :=
  (BitVec.append (old.drop n) new).setWidth _
def BitVec.replace {w1} (old : BitVec w1) (i : Nat) {w2} (new : BitVec w2) : BitVec w1 :=
  (old.extractLsb' (i + w2) (w1 - w2 - i) ++ new ++ old.extractLsb' 0 i).setWidth _

namespace Reg
def base {w} (r : Reg w) : Reg64 := match r with
  | .low r _ => r
  | .ah => .rax | .bh => .rbx | .ch => .rcx | .dh => .rdx

def offset {w} (r : Reg w) : Nat := match r with
  | .low _ _ => 0
  | .ah | .bh | .ch | .dh => 8
end Reg

structure Reg64s where
  rax : UInt64 := 0
  rbx : UInt64 := 0
  rcx : UInt64 := 0
  rdx : UInt64 := 0
  rsi : UInt64 := 0
  rdi : UInt64 := 0
  rsp : UInt64 := 0
  rbp : UInt64 := 0
  r8  : UInt64 := 0
  r9  : UInt64 := 0
  r10 : UInt64 := 0
  r11 : UInt64 := 0
  r12 : UInt64 := 0
  r13 : UInt64 := 0
  r14 : UInt64 := 0
  r15 : UInt64 := 0
  deriving Repr, BEq, DecidableEq, Hashable, Hashable, Lean.ToExpr

def Reg64s.get64 (s : Reg64s) (r : Reg64) : Width.W64.type := UInt64.toBitVec (match r with
  | .rax => s.rax | .rbx => s.rbx | .rcx => s.rcx | .rdx => s.rdx
  | .rsi => s.rsi | .rdi => s.rdi | .rsp => s.rsp | .rbp => s.rbp
  | .r8  => s.r8  | .r9  => s.r9  | .r10 => s.r10 | .r11 => s.r11
  | .r12 => s.r12 | .r13 => s.r13 | .r14 => s.r14 | .r15 => s.r15)

def Reg64s.set64 (regs : Reg64s) (r : Reg64) (v : Width.W64.type) : Reg64s :=
  let  v := UInt64.ofBitVec v
  match r with
  | .rax => { regs with rax := v } | .rbx => { regs with rbx := v }
  | .rcx => { regs with rcx := v } | .rdx => { regs with rdx := v }
  | .rsi => { regs with rsi := v } | .rdi => { regs with rdi := v }
  | .rsp => { regs with rsp := v } | .rbp => { regs with rbp := v }
  | .r8  => { regs with r8  := v } | .r9  => { regs with r9  := v }
  | .r10 => { regs with r10 := v } | .r11 => { regs with r11 := v }
  | .r12 => { regs with r12 := v } | .r13 => { regs with r13 := v }
  | .r14 => { regs with r14 := v } | .r15 => { regs with r15 := v }

def Reg64s.get (s : Reg64s) {w} (r : Reg w) : w.type :=
  ((s.get64 r.base).drop r.offset).take w.bits
  -- BitVec because it may be signed or unsigned depending on context

def Reg64s.set (s : Reg64s) {w} (r : Reg w) (v : w.type) : Reg64s := match r with
  | .low r .W64 => s.set64 r v
  | .low r .W32 => s.set64 r (v.zeroExtend _)
  | .low r w => s.set64 r ((s.get64 r).replaceLow v)
  | .ah | .bh | .ch | .dh => let old := s.get64 r.base;
    s.set64 r.base (old.replaceLow (BitVec.append v (s.get (.low r.base .W8))))

structure StatusFlags where
  cf : Bool
  pf : Bool
  af : Bool
  zf : Bool
  sf : Bool
  of : Bool
  deriving Repr, BEq, DecidableEq, Hashable, Lean.ToExpr

abbrev DataMem := Std.ExtHashMap UInt64 UInt64
instance : Repr DataMem where reprPrec _ _ := "<opaque memory>"
structure MachineData where -- does not include code or program position
  regs : Reg64s := {}
  status : StatusFlags := .mk false false false false false false
  dmem : DataMem := ∅
  deriving Repr, BEq, DecidableEq

abbrev MachineState := MachineData × Int64

-- We only allow nondeterministic choices for a fixed set of types.
class inductive NondetSupportingType : Type -> Type
  | bitvec (w : Width) : NondetSupportingType w.type
  | bool : NondetSupportingType Bool
  | statusFlags : NondetSupportingType StatusFlags

def NondetSupportingType.from_hash {α} [t : NondetSupportingType α] (h : UInt64) : α :=
  match t with
  | .bool => h % 2 != 0
  | .statusFlags => let h := h.toBitVec; (.mk h[0] h[1] h[2] h[3] h[4] h[5])
  | .bitvec w => h.toBitVec.setWidth w.bits

instance (w : Width) : NondetSupportingType w.type := .bitvec w
instance : NondetSupportingType Bool := .bool
instance : NondetSupportingType StatusFlags := .statusFlags

inductive Effects
  | done (s : MachineState)
  | undefined (msg : String)
  | unimplemented (msg : String)
  -- TODO add rip to the data that the effect handler can modify so that
  -- unsupported instructions can be handled by jumping to some handler,
  -- and make sure Instr.interp can deal with an arbitratily changed rip
  | unsupported_instruction (s : MachineData) (instr : Instr) (resume : MachineData → Effects)
  -- loads and stores *outside* the data memory, eg. MMIO, might still affect the data memory:
  -- for instance, MMIO reads/writes at certain device register addresses might change what
  -- data memory the process logically owns vs what memory is owned by devices
  | nonmem_load (dmem : DataMem) (addr : BitVec 64) (w : Width) (ret : w.type → DataMem → Effects)
  | nonmem_store (dmem : DataMem) (addr : BitVec 64) {w : Width} (v : w.type) (ret: DataMem → Effects)
  | pick (α : Type) [NondetSupportingType α] (ret : α → Effects)
  | can_read (addr : BitVec 64) (w : Width) (cont : Bool → Effects)
  | can_write (addr : BitVec 64) (w : Width) (cont : Bool → Effects)
  | can_exec (p: Std.Rco Int64) (cont : Bool → Effects)
export Effects (nonmem_load nonmem_store undefined unimplemented pick can_read can_write can_exec)

def Reg.interp {α w} (r : Reg w) (s : MachineData) (_ : Std.Rco Int64) (ret : w.type → α) :=
  ret (s.regs.get r) -- the unused argument is present ^ for uniformity with RegOrMem.interp

-- Since MMIO can cause devices to do arbitrary actions, a load might actually
-- *modify* memory.
-- But we don't want to model this full complexity everywhere, so we define a
-- full_load that we use for MOV, and a simple_load that we use for all
-- other instructions with memory operands.
-- Because of this simplification, our semantics would reject the following example:
-- A TEST instruction might load a flag from an MMIO address and bitwise-and it with
-- an immediate, and if the result is non-zero, it might mean that some device has
-- finished processing a buffer and therefore now passes ownership of that buffer
-- to the CPU.
-- Possible workarounds:
-- * replace the TEST-with-memory-operand by a MOV and a TEST-with-register-operand
-- * Change the signature of RegOrMem.interp to allow memory modification (considerable
--   refactoring)

def MachineData.generic_load
  (nonmem_load_param : DataMem → BitVec 64 → (w': Width) → (w'.type → DataMem → Effects) → Effects)
  (s : MachineData) (addr : BitVec 64) (w : Width)
  (ret : w.type → DataMem → Effects): Effects :=
  if addr % w.bytesv != 0 then .unimplemented s!"Unimplemented: only aligned memory access is supported"
  else can_read addr w (fun allowed =>
    if !allowed then .undefined s!"Memory read of {repr w} not allowed at address {repr addr}"
    else let key := UInt64.ofBitVec (addr &&& ~~~0b111#64)
    match s.dmem[key]? with
    | .some v => ret (v.toBitVec.extractLsb' ((addr &&& 0b111#64) * 8#64).toNat w.bits) s.dmem
    | .none => nonmem_load_param s.dmem addr w ret)

def MachineData.full_load :
  MachineData → BitVec 64 → (w : Width) → (w.type → DataMem → Effects) → Effects :=
  MachineData.generic_load nonmem_load

def MachineData.simple_load (s : MachineData) (addr : BitVec 64) (w : Width) (ret : w.type → Effects): Effects :=
  MachineData.generic_load nonmem_load_param s addr w (fun res _new_mem => ret res)
  where nonmem_load_param _dmem _addr _w _ret :=
    .unimplemented s!"Unimplemented: simple_load may not load outside of data memory, use full_load instead"

def MachineData.store (s : MachineData) (addr : BitVec 64) {w : Width} (v : w.type) (ret: MachineData → Effects) : Effects :=
  if addr % w.bytesv != 0 then .unimplemented s!"Unimplemented: only aligned memory access is supported"
  else can_write addr w (fun allowed =>
    if !allowed then .undefined s!"Memory write of {repr w} not allowed at address {repr addr}"
    else let key := UInt64.ofBitVec (addr &&& ~~~0b111#64)
    match s.dmem[key]? with
    | .some old =>
        let new := UInt64.ofBitVec (old.toBitVec.replace ((addr &&& 0b111#64) * 8#64).toNat v)
        ret { s with dmem := s.dmem.insert key new }
    | .none => nonmem_store s.dmem addr v (fun dmem' => ret { s with dmem := dmem' }))

class Labels where label : Label → Int64
export Labels (label)

def ConstExpr.interp [Labels] : ConstExpr → Std.Rco _root_.Int64 → _root_.Int64
  | .label l, _ => Labels.label l
  | .int64 i, _ => i
  | .before_current_instruction, r => r.lower
  | .after_current_instruction, r => r.upper
  | .add e1 e2, p => e1.interp p + e2.interp p
  | .sub e1 e2, p => e1.interp p - e2.interp p

def AddrExpr.interp [Labels] [address_size : AddressSize] (a : AddrExpr) (s : Reg64s) (p : Std.Rco Int64) :=
  let base := match a.base with
              | .some (.ofRegW ⟨_, r⟩)  => (s.get r).toInt
              | .some .rip => p.upper.toInt
              | .none => 0
  let idx := match a.idx with
             | .some ⟨⟨_, r⟩, c⟩ => (s.get r).toInt * c.bytes
             | .none => 0
  BitVec.ofInt address_size.address_size.bits (base + idx + (a.disp.interp p).toInt)

def RegOrMem.interp {w} [Labels] [AddressSize] (o : RegOrMem w) (s : MachineData) (p : Std.Rco Int64) (ret : w.type → Effects) :=
  match o with
  | .reg r => ret (s.regs.get r)
  | .mem a => s.simple_load ((a.interp s.regs p).zeroExtend _) w ret

def RegOrMem.full_interp {w} [Labels] [AddressSize] (o : RegOrMem w) (s : MachineData) (p : Std.Rco Int64) (ret : w.type → DataMem → Effects) :=
  match o with
  | .reg r => ret (s.regs.get r) s.dmem
  | .mem a => s.full_load ((a.interp s.regs p).zeroExtend _) w ret

def MachineData.setReg (s : MachineData) {w} (r : Reg w) (v : w.type) : MachineData :=
  { s with regs := s.regs.set r v }

def MachineData.set {w} [Labels] [AddressSize] (s : MachineData) (d : Dst w) (v : w.type) (p : Std.Rco Int64) (ret : MachineData → Effects) : Effects :=
  match d with
  | .reg r => ret (s.setReg r v)
  | .mem a => s.store ((a.interp s.regs p).zeroExtend _) v ret

def Operand.interp {w} [Labels] [AddressSize] (o : Operand w) (s : MachineData) (p : Std.Rco Int64) (ret : w.type → Effects) :=
  match o with
  | regOrMem rm => rm.interp s p ret
  | .imm v => ret ((v.interp p).toBitVec.truncate _)
  -- we rely on assemblers erroring out on too-large immediates in uniform ops

def Operand.full_interp {w} [Labels] [AddressSize] (o : Operand w) (s : MachineData) (p : Std.Rco Int64) (ret : w.type → DataMem → Effects) :=
  match o with
  | regOrMem rm => rm.full_interp s p ret
  | .imm v => ret ((v.interp p).toBitVec.truncate _) s.dmem
  -- we rely on assemblers erroring out on too-large immediates in uniform ops

def CondCode.interp (cc : CondCode) (s : StatusFlags) : Bool := match cc with
  | .z  => s.zf | .nz => !s.zf | .c  => s.cf | .nc => !s.cf
  | .a  => !s.cf && !s.zf | .be => s.cf || s.zf

def ShiftCountExpr.interp [Labels] (c : ShiftCountExpr) (s : MachineData) (p : Std.Rco Int64) := match c with
  | .cl => s.regs.rcx.toBitVec.take 8
  | .imm8 v => (v.interp p).toBitVec.truncate _
def ShiftCountExpr.interpMasked [Labels] (c : ShiftCountExpr) (s : MachineData) (p : Std.Rco Int64) (w : Width) : Nat :=
  (c.interp s p).toNat &&& match w with | .W64 => 0x3f | _ => 0x1f -- "masked to 5 bits (or 6 bits with a 64-bit operand)"

def RelRegOrMem.interp [Labels] [AddressSize] (o : RelRegOrMem) (s : MachineData) (p : Std.Rco Int64) (ret : BitVec 64 → Effects) :=
  match o with
  | .rel c => ret (p.upper + c.interp p).toBitVec
  | .reg r => ret (s.regs.get r)
  | .mem a => s.simple_load ((a.interp s.regs p).zeroExtend _) .W64 ret

structure StatusFlags.from_result.Remaining where
  cf : Bool
  af : Bool
  of : Bool
  deriving Repr, BEq, DecidableEq

-- TEMPORARY: definitions stolen from Lean 4.28's standard library, but with a
-- different name so that this file builds with both 4.27 and 4.28
namespace BitVec
def cpopNatRec_ {w} (x : BitVec w) (pos acc : Nat) : Nat :=
  match pos with
  | 0 => acc
  | n + 1 => x.cpopNatRec_ n (acc + (x.getLsbD n).toNat)

def cpop_ {w} (x : BitVec w) : BitVec w := BitVec.ofNat w (cpopNatRec_ x w 0)
end BitVec

def StatusFlags.from_result {w} (result : BitVec w) (f : from_result.Remaining) : StatusFlags :=
  { pf := (result.truncate 8).cpop_ % 2 == BitVec.zero _
    zf := result == BitVec.zero _
    sf := result.msb, cf := f.cf, af := f.af, of := f.of }



set_option maxHeartbeats 1000000
def Operation.interp [Labels] [address_size : AddressSize]
  {w} (i : Operation w) (p : Std.Rco Int64) (s : MachineData)
  (next : MachineData → Effects) (jmp : Int64 → MachineData → Effects)
  (unsupported : Effects) : Effects :=
  match (generalizing := false) (motive := Operation w → Effects) i with
  | .mov dst src => src.full_interp s p (fun val dmem' =>
      { s with dmem := dmem' }.set dst val p next)
  | .movsx dst src => src.full_interp s p (fun val dmem' =>
      { s with dmem := dmem' }.set dst (val.signExtend _) p next)
  | .movzx dst src => src.full_interp s p (fun val dmem' =>
      { s with dmem := dmem' }.set dst (val.zeroExtend _) p next)
  | .push src =>
    src.interp s p (fun v =>
    let rsp := s.regs.get64 .rsp - w.bytesv
    { s with regs := s.regs.set64 .rsp rsp }.store rsp v next)
  | .pop dst =>
    let rsp := s.regs.get64 .rsp
    s.simple_load rsp w (fun val =>
    let s := { s with regs := s.regs.set64 .rsp (rsp + w.bytesv) }
    s.set dst val p next)
  | .setcc cc dst =>
    s.set dst (cc.interp s.status) p next
  | .cmovcc cc dst src =>
    src.interp s p (fun src =>
    let v := if cc.interp s.status then src else s.regs.get dst
    next (s.setReg dst v))
-- Arithmetic
  | .lea dst src => next (s.setReg dst ((src.interp s.regs p).zeroExtend _))
  | .add dst src =>
    src.interp s p (fun a =>
    dst.interp s p (fun b =>
    let v := a + b
    let status := .from_result v {
      cf := v.toNat != a.toNat + b.toNat
      af := (v.truncate 4).toNat != (a.truncate 4).toNat + (b.truncate 4).toNat,
      of := v.toInt != a.toInt + b.toInt }
    { s with status }.set dst v p next))
  | .adc dst src =>
    src.interp s p (fun a =>
    dst.interp s p (fun b =>
    let c := s.status.cf
    let v := a + b + c
    let status := .from_result v {
      cf := v.toNat != a.toNat + b.toNat + c
      af := (v.truncate 4).toNat != (a.truncate 4).toNat + (b.truncate 4).toNat + c,
      of := v.toInt != a.toInt + b.toInt + c }
    { s with status }.set dst v p next))
  | .adcx dst src =>
    src.interp s p (fun a =>
    dst.interp s p (fun b =>
    let v := a + b + s.status.cf
    let cf := v.toNat != a.toNat + b.toNat + s.status.cf.toNat
    next { s with regs := s.regs.set dst v, status := { s.status with cf := cf }}))
  | .adox dst src =>
    src.interp s p (fun a =>
    dst.interp s p (fun b =>
    let v := a + b + s.status.of
    let of := v.toNat != a.toNat + b.toNat + s.status.of.toNat
    next { s with regs := s.regs.set dst v, status := { s.status with of := of }}))
  | .inc dst =>
    dst.interp s p (fun a =>
    let v := a + 1
    let status := .from_result v {
      cf := s.status.cf
      af := (v.truncate 4).toNat != (a.truncate 4).toNat + 1,
      of := v.toInt != a.toInt + 1 }
    { s with status }.set dst v p next)
  | .dec dst =>
    dst.interp s p (fun a =>
    let v := a - 1
    let status := .from_result v {
      cf := s.status.cf
      af := (v.truncate 4).toNat != (a.truncate 4).toNat - 1,
      of := v.toInt != a.toInt - 1 }
    { s with status }.set dst v p next)
  | .neg dst =>
    dst.interp s p (fun b =>
    let v := -b
    let status := .from_result v {
      cf := b != 0
      af := (b.truncate 4) != 0,
      of := v.toInt != - b.toInt }
    { s with status }.set dst v p next)
  | .sub dst src =>
    src.interp s p (fun a =>
    dst.interp s p (fun b =>
    let v := b - a
    let status := .from_result v {
      cf := v.toNat != b.toNat - a.toNat
      af := (v.truncate 4).toNat != (b.truncate 4).toNat - (a.truncate 4).toNat,
      of := v.toInt != b.toInt - a.toInt }
    { s with status }.set dst v p next))
  | .sbb dst src =>
    src.interp s p (fun a =>
    dst.interp s p (fun b =>
    let c := s.status.cf
    let v := b - a - c
    let status := .from_result v {
      cf := v.toNat != b.toNat - a.toNat - c.toNat
      af := (v.truncate 4).toNat != (b.truncate 4).toNat - (a.truncate 4).toNat - c.toNat,
      of := v.toInt != b.toInt - a.toInt - c.toInt }
    { s with status }.set dst v p next))
  | .cmp a b =>
    a.interp s p (fun a =>
    b.interp s p (fun b =>
    let v := b - a
    let status := .from_result v {
      cf := v.toNat != b.toNat - a.toNat
      af := (v.truncate 4).toNat != (b.truncate 4).toNat - (a.truncate 4).toNat,
      of := v.toInt != b.toInt - a.toInt }
    next { s with status }))
  | .mul src =>
    let a := s.regs.get (Reg.low .rax w)
    src.interp s p (fun b =>
    let v := a * b
    let vn := a.toNat * b.toNat
    let s := if w == .W8
      then s.setReg (.low .rax .W16) (.ofNat _ vn)
      else (s.setReg (.low .rax w) v).setReg (.low .rdx w) (.ofNat _ (vn >>> w.bits))
    pick Bool (λ sf => pick Bool (λ zf => pick Bool (λ af => pick Bool (λ pf =>
    next { s with status := { cf := v.toNat != vn, pf, af, zf, sf, of := v.toNat != vn }})))))
  | .mulx r_hi r_lo src1 =>
    src1.interp s p (fun a =>
    let b := s.regs.get (.low .rdx w)
    let v := a.toNat * b.toNat
    let s := s.setReg r_lo (.ofNat _ v) -- if r_hi = r_li, hi is written:
    let s := s.setReg r_hi (.ofNat _ (v >>> w.bits))
    next s)
  -- imul1 and imul collectively describe variants of the same
  -- syntax level `imul` instruction, where imul1 is the 1-operand case
  | .imul1 src =>
    let a := s.regs.get (Reg.low .rax w)
    src.interp s p (fun b =>
    let v := a.toInt * b.toInt
    let s := if w == .W8 then
      s.setReg (.low .rax .W16) (BitVec.ofInt 16 v)
    else
      let result := BitVec.ofInt (w.bits * 2) v
      let low := result.take w.bits
      let high := (result.drop w.bits).setWidth _
      (s.setReg (.low .rax w) low).setReg (.low .rdx w) high
    pick Bool (λ sf => pick Bool (λ zf => pick Bool (λ af => pick Bool (λ pf =>
    let low := BitVec.ofInt w.bits v
    let cf := v != low.toInt
    next { s with status := { cf := cf, pf, af, zf, sf, of := cf }})))))
  | .imul dst src1 src2 =>
    src1.interp s p (fun a =>
    src2.interp s p (fun b =>
    let v := a * b
    s.set (match (generalizing := false) (motive := Option (RegOrMem w) → RegOrMem w)
             dst with | .some dst => dst | _ => src1) v p (fun s =>
    let cf := v.toInt != a.toInt * b.toInt
    pick Bool (λ sf => pick Bool (λ zf => pick Bool (λ af => pick Bool (λ pf =>
    next { s with status := { cf := cf, pf, af, zf, sf, of := cf }})))))))
-- Bitwise
  | .test a b =>
    a.interp s p (fun a =>
    b.interp s p (fun b =>
    let v := a &&& b
    pick Bool (fun af =>
    let status := .from_result v { cf := false, af, of := false}
    next { s with status})))
  | .and dst src | .or dst src | .xor dst src =>
    dst.interp s p (fun a =>
    src.interp s p (fun b =>
    let v := match i with | .and _ _ => a &&& b | .or _ _ => a ||| b | _ => a ^^^ b
    pick Bool (fun af =>
    let status := .from_result v { cf := false, of := false, af }
    { s with status }.set dst v p next)))
  | .not dst =>
    dst.interp s p (fun a =>
    let v := ~~~a
    s.set dst v p next)
  | .shl dst count =>
    dst.interp s p (fun a =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let v := a <<< count
    pick Bool (λ af =>
    (λ setcf => if count < w.bits then setcf (a <<< (count-1)).msb else pick Bool setcf) (λ cf =>
    (λ setof => if count == 1 then setof (v.msb != a.msb) else pick Bool setof) (λ of =>
    { s with status := .from_result v { s.status with cf, af, of } }.set dst v p next))))
  | .shr dst count =>
    dst.interp s p (fun a =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let v := a.ushiftRight count
    pick Bool (λ af =>
    (λ setcf => if count < w.bits then setcf (a.getLsbD (count-1)) else pick Bool setcf) (λ cf =>
    (λ setof => if count == 1 then setof a.msb else pick Bool setof) (λ of =>
    { s with status := .from_result v { s.status with cf, af, of } }.set dst v p next))))
  | .sar dst count =>
    dst.interp s p (fun a =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let v := a.sshiftRight count
    pick Bool (λ af =>
    (λ setcf => if count < w.bits then setcf (a.getLsbD (count-1)) else pick Bool setcf) (λ cf =>
    (λ setof => if count == 1 then setof false else pick Bool setof) (λ of =>
    { s with status := .from_result v { s.status with cf, af, of } }.set dst v p next))))
  | .shrd dst src count =>
    dst.interp s p (fun a =>
    src.interp s p (fun b =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let v := (((b.append a) >>> count).take w.bits).setWidth _
    (λ setstatus => if count >= w.bits then pick StatusFlags setstatus else
      let cf := a.getLsbD (count-1)
      pick Bool (λ af =>
      (λ setof => if count == 1 then setof (v.msb != a.msb) else pick Bool setof) (λ of =>
      setstatus (.from_result v { cf, af, of})))) (λ status =>
    { s with status }.set dst v p next)))
  | .shld dst src count =>
    dst.interp s p (fun a =>
    src.interp s p (fun b =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let v := (((a.append b) <<< count).drop w.bits).setWidth _
    (λ setstatus => if count >= w.bits then pick StatusFlags setstatus else
      let cf := (a <<< (count-1)).msb
      pick Bool (λ af =>
      (λ setof => if count == 1 then setof (v.msb != a.msb) else pick Bool setof) (λ of =>
      setstatus (.from_result v { cf, af, of})))) (λ status =>
    { s with status }.set dst v p next)))
  | .rol dst count =>
    dst.interp s p (fun a =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let v := a.rotateLeft count
    let cf := v.getLsbD 0
    (λ setof => if count == 1 then setof (v.msb != a.msb) else pick Bool setof) (λ of =>
    { s with status := { s.status with cf, of } }.set dst v p next))
  | .ror dst count =>
    dst.interp s p (fun a =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let v := a.rotateRight count
    let cf := v.msb
    (λ setof => if count == 1 then setof (v.msb != a.msb) else pick Bool setof) (λ of =>
    { s with status := { s.status with cf, of } }.set dst v p next))
  | .rcr dst count =>
    dst.interp s p (fun a =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let t := (BitVec.ofBool s.status.cf ++ a).rotateRight count
    let (cf, v) := (t.msb, t.take w.bits)
    (λ setof => if count == 1 then setof (v.msb != a.msb) else pick Bool setof) (λ of =>
    { s with status := { s.status with cf, of } }.set dst v p next))
  | .rcl dst count =>
    dst.interp s p (fun a =>
    let count := count.interpMasked s p w
    if count == 0 then next s else
    let t := (BitVec.ofBool s.status.cf ++ a).rotateLeft count
    let (cf, v) := (t.msb, t.take w.bits)
    (λ setof => if count == 1 then setof (v.msb != a.msb) else pick Bool setof) (λ of =>
    { s with status := { s.status with cf, of } }.set dst v p next))
  | .bswap dst =>
    let a := s.regs.get dst
    match (generalizing := false) (motive := Width → Effects) w with
    | .W32 =>
      let v := a.take 8 ++ a.extractLsb' 8 8 ++ a.extractLsb' 16 8 ++ a.drop 24
      next (s.setReg dst (v.setWidth _))
    | .W64 =>
      let v := a.take 8 ++ a.extractLsb' 8 8 ++ a.extractLsb' 16 8 ++ a.extractLsb' 24 8
            ++ a.extractLsb' 32 8 ++ a.extractLsb' 40 8 ++ a.extractLsb' 48 8 ++ a.drop 56
      next (s.setReg dst (v.setWidth _))
    | _ => pick w.type (fun v => next (s.setReg dst v))
  | .jcc cc l =>
    if cc.interp s.status
    then jmp (label l) s
    else next s
  | .jmp tgt =>
    tgt.interp s p (fun a =>
    jmp (.ofBitVec a) s)
  | .call tgt =>
    tgt.interp s p (fun a =>
    let rsp := s.regs.get64 .rsp - Width.W64.bytesv
    { s with regs := s.regs.set64 .rsp rsp }.store rsp (w:=.W64) p.upper.toBitVec (jmp (.ofBitVec a)))
  | .ret =>
    let rsp := s.regs.get64 .rsp
    s.simple_load rsp .W64 (fun ra =>
    jmp (.ofBitVec ra) { s with regs := s.regs.set64 .rsp (rsp + 8) })
  | nop _ | nopalign _ _ => next s
  | .generic .. => unsupported

def Instr.interp [Labels]
  (i : Instr) (s : MachineData) (p : Std.Rco Int64)
  (next : MachineData → Effects) (jmp : Int64 → MachineData → Effects) : Effects :=
  can_exec p (fun allowed =>
    if allowed
    then Operation.interp
            (w := i.operation_size) (address_size := .mk i.address_size)
            i.operation p s next jmp
            (Effects.unsupported_instruction s i next)
    else .undefined s!"No exec permissions at {repr p.lower}..{repr p.upper}")

def Directive.interp [Labels]
  (d : Directive) (s : MachineData) (p : Std.Rco Int64)
  (next : MachineData → Effects) (jmp : Int64 → MachineData → Effects) : Effects :=
  match d with
  | .label _ => next s
  | .instr i => i.interp s p next jmp
  | .byteArray _ => .unimplemented s!"Unimplemented: execution reached data block at {p.1}"

def Directives.interp [Labels]
  (ds : List (Directive × Nat)) (s : MachineData) (pc : Int64)
  (ret : Int64 → MachineData → Effects) : Effects :=
  match ds with
  | [] => ret pc s
  | (d, sz) :: ds =>
    d.interp s (.mk pc (pc+.ofNat sz)) (jmp:=ret) (next := (fun s =>
    interp ds s (pc+.ofNat sz) ret))

class Layout where (start : Int64) (size : Nat → Nat)
def Layout.apply (l : Layout) (prog : Program) : Executable :=
  (l.start, prog.mapIdx (fun i d => (d, l.size i)))
instance : CoeFun Layout (fun _ => Program → Executable) where coe := Layout.apply

-- TEMPORARY: delete when dropping support for Lean <= 4.28
-- (above which Init.Data.List.scan.Basic is supported).
-- This namespace permits use of Mathlib 4.27 which also implements its own
-- `scanl` which differs from the one here.
namespace Kraken.Compat
@[inline]
private def scanAuxM {α β m} [Monad m] (f : β → α → m β) (init : β) (l : List α) : m (List β) :=
  go l init []
where
  @[specialize] go : List α → β → List β → m (List β)
    | [], last, acc => pure <| last :: acc
    | x :: xs, last, acc => do go xs (← f last x) (last :: acc)
@[inline]
def scanlM {α β m} [Monad m] (f : β → α → m β) (init : β) (l : List α) : m (List β) :=
  List.reverse <$> scanAuxM f init l
@[inline]
def scanl {α β} (f : β → α → β) (init : β) (as : List α) : List β :=
  Id.run <| Kraken.Compat.scanlM (pure <| f · ·) init as
end Kraken.Compat

def Executable.withAddresses (e : Executable)  : List (Int64 × Directive × Nat) :=
  (Kraken.Compat.scanl (fun (p, _, _) (d, z) => (p+.ofNat z, d, z)) (e.1, .byteArray (.mk #[]), 0) e.2)

def Executable.labels (e : Executable) : Labels :=
  { label l := (e.withAddresses.findSome?
      (fun (p, d, _) => if d = .label l then .some p else .none)).getD (-1) }

def Executable.directivesAtAddress (e : Executable) (a : Int64) : List (Directive × Nat) :=
  (e.withAddresses.filter (·.1 = a)).map (·.2)

def Executable.directivesFromAddress (e : Executable) (a : Int64) : List (Directive × Nat) :=
  e.2.drop (((e.withAddresses).map (·.1)).idxOf a)

def Executable.directivesFromLabel (e : Executable) (l : Label) : List (Directive × Nat) :=
  e.2.dropWhile (·.1 != .label l)

def Executable.step (e : Executable) (s : MachineState) (ret : MachineState → Effects) : Effects :=
  let := e.labels
  Directives.interp (e.directivesAtAddress s.2) s.1 s.2 (fun pc s => ret (s, pc))

def Executable.straightline (e : Executable) (s : MachineState) (ret : MachineState → Effects) : Effects :=
  let := e.labels;
  Directives.interp (e.directivesFromAddress s.2) s.1 s.2 (fun pc s => ret (s, pc))

-- -- Concrete evaluators for expedient testing

partial def Executable.eval (e : Executable) (s : MachineState) (until_ : MachineState → Bool) : Except String (MachineState) :=
  if until_ s then .ok s else handle_effects (e.straightline s .done)
where
  handle_effects es :=
    match es with
    | .done s => eval e s until_
    | .undefined msg => .error msg
    | .unimplemented msg => .error msg
    | .unsupported_instruction _ i _ => .error s!"unsupported instruction {repr i}"
    | .can_read _ _ cont => handle_effects (cont true)
    | .can_write _ _ cont => handle_effects (cont true)
    | .can_exec _ cont => handle_effects (cont true)
    | .nonmem_load addr .. => .error s!"Load at unmapped address {repr addr}"
    | .nonmem_store addr .. => .error s!"Store at unmapped address {repr addr}"
    | @Effects.pick _ t cont => handle_effects (cont (t.from_hash (hash s.1.regs)))

def Directive.fakeSize (hashOfProgram : UInt64) (d : Directive) : Nat :=
  match d with
  | .label _ => 0
  | .instr (.mk _ _ (.nop sz)) => sz -- may be zero
  | .instr i => (1 + hash (hashOfProgram, i) % 15).toNat
  | .byteArray bs => bs.size

def Program.fakeLayout (prog : Program) : Executable :=
  let : Inhabited Directive := .mk (.byteArray (.mk #[]))
  let h := hash prog;
  let layout : Layout := { start := h.toInt64<<<16, size i := prog[i]!.fakeSize h }
  layout prog

abbrev eval [layout : Layout] (prog : Program) := (layout prog).eval

/-- info: Except.ok 42 -/
#guard_msgs in
#eval
  let exe := Program.fakeLayout [
    .label "main",
    .instr (.mk .W64 .W64 (.lea (.low .rax .W64) (.mk .none .none (.int64 41)))),
    .instr (.mk .W64 .W64 (.inc (.reg (.low .rax .W64)))),
    .instr (.mk .W64 .W64 .ret) ]
  let start := exe.labels.label "main"
  let data := { dmem := .ofList [(0x100, 0x1337)], regs := {rsp := 0x100} }
  (exe.eval (data, start) (fun (_, pc) => pc = 0x1337)).bind (fun s => .ok s.1.regs.rax)
